<ol class="bread" itemscope itemtype="http://schema.org/BreadcrumbList">
  <li itemprop="itemListElement" itemscope itemtype="http://schema.org/ListItem">
    <a itemprop="item" href="/">
        <span itemprop="name">Perl</span></a>
    <meta itemprop="position" content="1" />
  </li>
  &#8250;
  <li itemprop="itemListElement" itemscope itemtype="http://schema.org/ListItem">
    <a itemprop="item" href="/blog/20101130129876.html">
        <span itemprop="name">サブルーチン</span></a>
    <meta itemprop="position" content="2" />
  </li>
  &#8250;
  <li itemprop="itemListElement" itemscope itemtype="http://schema.org/ListItem">
      <span itemprop="name">here</span>
    <meta itemprop="position" content="3" />
  </li>
</ol>
<h2 class="title">クロージャーを作成する方法</h2>



Perlで「クロージャー」を作成する方法を解説します。

<h3>クロージャの定義</h3>

クロージャの定義をコードを使って解説します。

<pre>
use strict;
use warnings;

# もっともシンプルなクロージャ
{
  # 変数の生成
  my $var = 5;
  sub var {
    return $var;
  }
}
# スコープが終わってもvarサブルーチンに参照されているため、
# $var は存在し続ける。
# var サブルーチンだけが、$var を見ることが可能。

print "1: もっともシンプルなクロージャ\n";
print var(), "\n";
</pre>

<h4>クロージャの定義</h4>

クロージャであるための条件は、サブルーチンが、自分のスコープ外のレキシカル変数を参照していることです。( この例では、var が、自分のスコープ外の$var を参照。)

クロージャの定義は人や状況によってあいまいです。varサブルーチンのことをクロージャと呼ぶこともありますし、「varサブルーチンと参照されている変数$var を含む環境」のことをクロージャと呼ぶこともあります。

サブルーチンを作成するときは、必ず引数として変数を受け取って、スコープ外の変数を参照してはいけないと書きましたが、クロージャは例外のひとつです。

Perlのスコープについては以下の記事を参考にしてください。

<ul>
  <li>
    <a href="/blog/20110128130016.html">Perlのスコープについて理解しよう</a>
  </li>
</ul>

<h4>クロージャの特徴</h4>

<pre>
{
  # 変数の生成
  my $var = 5; 
  sub var {
    return $var;
  }
}
</pre>

$var は、varサブルーチン内で参照されているために、スコープが終了しても、メモリ上から削除されない。( レキシカル変数は、スコープが終わって、どこからも参照されていないときにメモリ上から破棄される。）

スコープが終了すると、スコープ外からは、$var が見えなくなる。( varサブルーチンは、スコープの影響を受けないため、スコープ外からは見える。)

$var を見ることができるのは、var サブルーチンだけである。スコープが終了した後は、var サブルーチンを通してのみ、 $var の値にアクセスすることができる。

<h4>クロージャのイメージ</h4>

<pre>
|-------------スコープ( クロージャ )--------------|
|                                                 |
| |--------|              |--------------|        |     |------------|
| |        | アクセス可能 |              | アクセス可能 |            |
| | データ |<-------------| サブルーチン |<-------------| スコープ外 |
| |        |              |              |        |     |            |
| |--------|              |--------------|        |     |------------|
|                                                 |
|-------------------------------------------------|

                   スコープ外からデータにはアクセス不能
</pre>

スコープ外からは、サブルーチンを通じてのみデータにアクセス可能。直接データにアクセスすることができない。

<h3>クロージャを「関数ジェネレータ」を使って作成する</h3>

クロージャを関数ジェネレータを使って作成してみます。

<pre>
use strict;
use warnings;

# タイマーを生成する。
print "1: 関数ジェネレータでクロージャを生成する( タイマー )。\n";
my $timer1 = create_timer(); 
sleep 1;
# 1秒後にもうひとつタイマーを生成
my $timer2 = create_timer();

# 1秒止まる
sleep 1;

print '$timer1 による経過秒: ';
print $timer1->(), "\n";

print '$timer2 による経過秒: ';
print $timer2->(), "\n\n";


# 経過時刻を知るクロージャを生成する関数ジェネレータ
sub create_timer {
  # time() は、関数ジェネレータが呼び出された時刻
  my $time = time(); 
  
  return sub {
    # time() は生成されたサブルーチンが
    # 呼び出された時刻。time() - $time で
    # 経過時刻を知ることができる。
    return time() - $time;                          
  }
}
</pre>

<h4>クロージャを生成するための関数ジェネレータ</h4>

<pre>
sub create_timer {
  # time() は、関数ジェネレータが呼び出された時刻
  my $time = time(); 

  # time() は生成されたサブルーチンが
  # 呼び出された時刻。time() - $time で
  # 経過時刻を知ることができる。
  return sub {
    return time() - $time;                      
  }
}
</pre>

関数ジェネレータとは、サブーチンを生成するサブルーチンのことです。関数ジェネレータを利用すれば、複数のクロージャを生成することができます。

生成されるそれぞれのクロージャは、データを独自に持ちます。この例では、 $time の値は、メモリ上にクロージャ毎に作成されます。戻り値はサブルーチンへのリファレンスになっています。これは、Perlでは、サブルーチンそのものを戻り値にすることができないためです。

<h4>関数ジェネレータの呼び出してクロージャを作成する</h4>

<pre>
my $timer1 = create_timer();
my $timer2 = create_timer();
</pre>

戻り値は、サブルーチンへのリファレンスになります。このサブルーチンはクロージャで,独自にメモリ上に環境( サブルーチンに参照された変数とサブルーチンの定義  )を持ちます。

<h4>クロージャを呼び出す</h4>

<pre>
$timer1->();
</pre>

サブルーチンへのリファレンスをうけったので、デリファレンスして呼び出します。

<h4>クロージャの利用方法</h4>

クロージャを利用すると、「基準になる時点での状態」と「現時点での状態」の変化を簡潔に記述することができます。

クラスを作れば同じことを実現できますが、クラスを作りたくない場合は、クロージャを利用します。( クラスをひとつ作れば、クラスを管理する手間が増えます。この例のようにシンプルな記述のためにわざわざクラスを作りたくない場合に、クロージャを使います。 )

<h3>クロージャに参照される「変数を初期化」する</h3>

クロージャに参照される変数を初期化するサンプルです。

<pre>
use strict;
use warnings;

# 関数ジェネレータに引数として渡す。
print "1: クロージャに参照される変数を初期化する。\n";
my $sign_inversion = create_sign_checker(1); 

if ($sign_inversion->(-2)) {
  # 符号が反転していたら
  print "-2 は、1から見て符号が反転しています。\n";
}

# 符号が反転しかかどうかを確かめるクロージャを生成する関数ジェネレータ
sub create_sign_checker {
  # クロージャを作成したときに初期化される。
  my $num1 = shift; 
    
  return sub {
    # クロージャを呼び出したときの引数。
    my $num2 = shift; 
    
    if ($num1 * $num2 > 0) {
      # 反転していない場合
      return 0; 
    }
    elsif ($num1 * $num2 < 0) {
      # 反転している場合
      return 1; 
    }
    else {
      # どちらかが0の場合undefを返す
      return; 
    }
  }
}
</pre>

<h4>クロージャに参照される変数を初期化する</h4>

<pre>
# クロージャを生成。( クロージャに参照される変数を1で初期化 )
my $sign_inversion = create_sign_checker(1);

# クロージャを呼び出す
$sign_inversion->(-2);

# クロージャを生成する関数ジェネレータ
sub create_sign_checker {
  # クロージャを作成したときに初期化される。
  my $num1 = shift; 
    
  # クロージャの定義
  return sub{
    # クロージャを呼び出したときの引数。
    my $num2 = shift; 
    # ...
  }
}
</pre>

クロージャを作成するときに、クロージャが参照する変数を初期化することができます。クロージャを作成するときに引数として渡します。それを、関数ジェネレータの引数として受け取って、変数に設定します。

<h3>クロージャの概念をクラスとの対比でわかりやすく説明する</h3>

クロージャの概念はぼやっとしていて輪郭を捕らえるのが難しいです。わたしなりにできるだけ理解できるように解説したいと思います。

<h4>クロージャとは</h4>

<ol>
  <li>
    クロージャーとは、データとそのデータを操作する関数のセットである。
  </li>
  <li>
    データはカプセル化されていて、データを操作できるのは、クロージャ内部の関数だけである。
  </li>
  <li>
    データの初期化が可能である。
  </li>
  <li>
    データごとに個々のインスタンスを作成することができる。
  </li>
  <li>
    関数はインスタンスから呼び出される。
  </li>
</ol>

次に、オブジェクト指向におけるクラスについて。

<h4>クラスとは</h4>

<ol>
  <li>
    クラスとは、データとそのデータを操作する関数のセットである。
  </li>
  <li>
    データはカプセル化されていて、データを操作できるのは、クラス内部の関数だけである。
  </li>
  <li>
    データの初期化が可能である。
  </li>
  <li>
    データごとに個々のインスタンスを作成することができる。
  </li>
  <li>
    関数はインスタンスから呼び出される。
  </li>
</ol>

ここで気づいてほしいことは、クラスとクロージャは、実は同じものだということです。Perlの実装においては、クラスとクロージャは、文法的な書き方において違いがあります。けれど、機能的には同じものです。クロージャとは簡易なクラスのことだと思えば理解しやすいです。(クロージャの概念は、もう少し広いけれど、最初のうちは簡易なクラスだと理解する。)

<h4>クラスとクロージャの相違点</h4>

<ol>
  <li>
    クロージャは、クラス名を持たない。my $ins = Class->new; というnewを呼び出すことで、インスタンスを生成するのではなく、 my $ins = closure_maker(); という形でインスタンスを生成する。
  </li>
  <li>
    クロージャは、継承できない。クラスのように継承をすることはできません。
  </li>
</ol>

クラスとクロージャを使って、数を数えるための機能(カウンター)を実装してみます。

<h4>カウンターをクラスで実装する</h4>

<pre>
use strict;
use warnings;

package Counter;
sub new{
  my $class = shift;
  my $self = {};
  $self->{cnt} = shift;
  bless $self, $class;
}

sub add_count{
  my $self = shift;
  $self->{cnt}++;
}

sub count{
  my $self = shift;
  return $self->{cnt};
}

package main;
my $cnt = Counter->new(10);

print "1: クラスによるカウンターの実装\n";
print "カウント前:", $cnt->count, "\n";
$cnt->add_count;
print "カウント後:", $cnt->count, "\n\n";
</pre>

<h4>カウンターをクロージャで実装する</h4>

<pre>
use strict;
use warnings;

# クロージャ
sub make_counter{
  my $self = {};
  $self->{cnt} = shift;
   
  # クロージャでは、サブルーチンへのリファレンスを使ってメソッドを実装する。
  my $funcs = {}; 
  $funcs->{add_count} = sub {
    $self->{cnt}++;
  };
   
   $funcs->{count} = sub {
     return $self->{cnt};
   };
   return $funcs;
}

my $cnt = make_counter(10);

print "2: クロージャによるカウンターの実装\n";
print "カウント前:" . $cnt->{count}->() . "\n"; 
$cnt->{add_count}->();
print "カウント後:" . $cnt->{count}->() . "\n";
</pre>

このようにクラスとクロージャは、よく似ています。次回は、コードを解説します。

<h4>クラスとクロージャの視覚的なイメージ</h4>

<pre>
## クラス
|------クラス------|
| ・データ         |
| ・データ操作関数 |
|------------------|


## クロージャ
|----クロージャ----|
| ・データ         |
| ・データ操作関数 |
|------------------|

</pre>

<h4>小飼 弾さんの添削</h4>

<ul>
  <li>
    <a href="http://blog.livedoor.jp/dankogai/archives/51079991.html">perl - Class vs. Closure(404 Blog Not Found)</a>
  </li>
  <li>
    クロージャでクラスと同じことができるけれど、Perlではクロージャの生成は、オブジェクトの生成よりもコストがかかるそうです。
  </li>
  <li>
    クロージャは、継承できないと書きましたが継承できるみたいです。(やりかたは今のところは思いつきません。)
  </li>
</ul>
