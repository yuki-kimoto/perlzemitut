<ol class="bread" itemscope itemtype="http://schema.org/BreadcrumbList">
  <li itemprop="itemListElement" itemscope itemtype="http://schema.org/ListItem">
    <a itemprop="item" href="/">
        <span itemprop="name">Perl</span></a>
    <meta itemprop="position" content="1" />
  </li>
  &#8250;
  <li itemprop="itemListElement" itemscope itemtype="http://schema.org/ListItem">
    <span itemprop="name">here</span>
  </li>
</ol>
<h2>サブルーチンの作成方法</h2>



サブルーチンの作成方法を学びましょう。サブルーチンとは、あるまとまった機能に名前をつけて呼び出せるようにする機能のことです。他の言語で関数と呼ばれる機能と同じです。Perlの標準関数については<a href="/blog/20161004147558.html">Perlの標準関数</a>で解説しています。

<h3>サブルーチンの定義</h3>

サブルーチンを定義するには次のようにします。

<pre>
sub サブルーチン名 {
  # 処理
}
</pre>

簡単なサブルーチンとして二つの値の和を求めるtotalという名前をつけたサブルーチンを定義してみましょう。

<pre>
sub total {
  # (1)引数の受け取り
  my ($num1, $num2) = @_;

  # (2)処理
  my $total = $num1 + $num2;

  # (3)戻り値
  return $total;
}
</pre>

(1)引数は「<strong>@_</strong>」という変数に格納されています。引数にわかりやすい名前をつけるために、$num1と$num2という変数に代入しなおしています。(2)二つの値を合計しています。(3)戻り値は<a href="/blog/20171111151040.html">return関数</a>を使って返却することができます。

<h3>サブルーチンの呼び出し</h3>

サブルーチンを呼び出すには次のようにします。引数を渡して、処理した結果を戻り値として受け取ることができます。

<pre>
戻り値 = サブルーチン(引数);
</pre>

では先ほど作成した、total関数を呼び出してみましょう。

<pre>
use strict;
use warnings;

# (1)引数に渡す値
my $num1 = 1;
my $num2 = 2;

# (2)サブルーチンの呼び出し
my $total = total($num1, $num2);

# (3)サブルーチンの定義
sub total {
  my ($num1, $num2) = @_;

  my $total = $num1 + $num2;

  return $total;
}
</pre>

(1)引数に渡すための値です。(2)サブルーチンの呼び出しです。戻り値は$totalに代入されます。1と2を足した結果が返ってくるので$totalは3になります。(3)サブルーチンの定義は、呼び出しの下におくことも可能です。サブルーチンの定義の読み込みは、コンパイル時に行われるからです。

<h3>サブルーチンの引数</h3>

サブルーチンの引数の受け取り方について解説します。

<h4>ひとつの引数を受け取る</h4>

ひとつの引数を受け取るときによく使われるのはshift関数を使う方法です。

<pre>
sub func {
  my $arg = shift;
}
</pre>

サブルーチンの中でshift関数を呼び出したときは「@_」がshift関数の暗黙の引数になります。つまり上記は

<pre>
sub func {
  my $arg = shift @_;
}
</pre>

と同じになります。

<h4>複数の引数を受け取る</h4>

複数の引数を受け取るときによく使われるのはリスト代入という方法です。

<pre>
sub func {
  my ($arg1, $arg2) = @_;
}
</pre>

@_のひとつ目の要素は$arg1に、ふたつめの要素は$arg2に代入されます。

また引数が同一種類の場合は、配列に受け取るようにします。

<pre>
sub func {
  my @args = @_;
}
</pre>

<h4>引数に直接アクセスする</h4>

「@_」は配列ですが、この配列に直接アクセスすることも可能です。

<pre>
#第一引数
$_[0]

#第二引数
$_[1]
</pre>

この書き方は可読性を下げますので、パフォーマンスがどうしても必要という場合以外は使わないことをお勧めします。

<h4>サブルーチンの引数に配列を渡す</h4>

サブルーチンに、配列を渡すことができます。

<pre>
# 渡す側
my $num_total = sum(@nums);

#受け取る側
sub sum{
  my @nums = @_; 
  # 処理
}
</pre>

<h4>サブルーチンの引数にハッシュを渡す</h4>

サブルーチンに、ハッシュを渡すこともできます。受け取る側では、@_をハッシュに代入してあげます。Perlでは配列の中身はリストですので、ハッシュに代入することができます。

<pre>
# 渡す側
my $examin_total = sum_examin(%examin); 

# 受け取る側
sub sum_examin{
  my %examin = @_;
  # 処理
}
</pre>

<h4>サブルーチンに複数の配列を渡したい</h4>

サブルーチンに複数の配列を渡したい場合は配列のリファレンスとして渡す必要があります。

<pre>
my @array1;
my @array2;

func(\@array1, \@array2);

sub func {
  my ($array1, $array2) = @_;
}
</pre>

以下のように二つの配列を渡してしまうとひとつの配列になってしまいうまくいきません。

<pre>
my @array1;
my @array2;

func(@array1, @array2);

sub func {
  # ひとつの配列になってしまう
  my @array = @_;
}
</pre>

<h4>サブルーチンに配列とハッシュを区別して渡す</h4>

サブルーチンに配列とハッシュを両方わたしたいときはどうすればいいでしょうか。そのような場合は、配列のリファレンス、ハッシュのリファレンスを使う必要があります。

<pre>
# 渡す側
my $max_num = summary([1, 2, 3], {how => 'max'});

# 受け取る側
sub summary {
  my ($nums, $option) = @_; 
  # 処理                            
}
</pre>

<h4>サブルーチンの引数で「ハッシュ」と「ハッシュのリファレンス」の両方を受け取れるようにする</h4>

サブルーチンでハッシュあるいはハッシュのリファレンスを受け取るには次のように記述します。

<pre>
func(name => 'Ken', age => 19);
func({name => 'Ken', age => 19});

sub func {
  # 場合に応じてハッシュのリファレンスに変換
  my $arg = ref $_[0] eq 'HASH' ? $_[0] : {@_};
}
</pre>

引数がハッシュのリファレンスだった場合は、そのまま代入をおこない、ハッシュだった場合は、ハッシュのリファレンスに変換します。$_[0]は引数である@_の一つ目の要素です。

<h3>引数の受け取り方の典型的なパターン</h3>

典型的な引数の受け取り方のパターンを紹介します。

<h4>(1) 複数のスカラー</h4>
<pre>
search('aiueo', 'eo', 2);

sub search {
  my ( $str, $search, $offset ) = @_;
}
</pre>

リストで受け取って、受け取り側でリストのそれぞれの値をスカラーに代入します。

<h4>(2) シンプルなリスト</h4>

<pre>
average(1 ,2 ,3 ,4 ,5);

sub average {
  my @nums = @_;
}
</pre>

リストで受け取って、配列に代入します。

<h4>(3) ひとつのスカラーとリスト</h4>

<pre>
search_file('file', 'apple', 'dog');

sub search_file {
  my ($file, @search_list) = @_;
}
</pre>

第1引数はスカラーに代入、第2引数以降は配列に代入します。

<h4>(4) シンプルなハッシュ</h4>

<pre>
search_arg_hash(str => 'aiueo', search => 'eo', offset => 2);

sub search_arg_hash{
  my %arg = @_;
  my ($str, $search, $offset) = @arg{'str', 'search', 'offset'};
}
</pre>

受け取り側で、ハッシュに代入します。引数の個数が増えた場合は、ハッシュを用いて、引数に名前を与えてあげたほうが、親切なインターフェイスになります。

ハッシュをスカラーに代入しなおす必要は必ずしもありませんが、代入しなおしたほうが見やすくコーディングしやすくなることが多いです。

<h4>(5) ひとつのスカラーとハッシュ</h4>

<pre>
parse_file('in_file', out_file => 'out_file', search_path => 'dir');

sub parse_file{
  my ($in_file, %opt) = @_;
  my ($out_file, $search_path) = @opt{'out_file', 'search_path'};
  return;
}
</pre>

第１引数をスカラーに、第２引数以降をハッシュに代入します。第一引数が必須の引数で、そのほかの引数はオプションと考えられる場合は、このインターフェイスが使えます。

<h3>サブルーチンの戻り値</h3>

<h4>スカラ値を戻り値として返却</h4>

サブルーチンの戻り値としてはスカラ値を返すのが基本です。

<pre>
sub func {
  my $name = 'Ken';

  return $name;
}
</pre>

<h4>配列やハッシュを戻り値として返却</h4>

Perlではリスト(配列やハッシュ)を返却することもできます。

<pre>
func {
  my @nums = (1, 2, 3);

  return @nums;
}
</pre>

ただし、この方法はあまりお勧めしません。以下で解説する「配列のリファレンス」「ハッシュのリファレンス」を返す方法をお勧めします。

また<a href="/blog/20080610121319.html">wantarray関数</a>で、呼び出しもとのコンテキストに応じて引数を返却することもできますが、余りお勧めしません。コンテキストに応じて戻り値を変えるとユーザは、スカラコンテキストを強制するために、ある位置では「scalar func()」という記述をせざるを得なくなるのが理由です。

<h4>リファレンスを戻り値として返却</h4>

配列のリファレンスやハッシュのリファレンスもスカラ値ですので戻り値として返すことができます。

<pre>
# 戻り値として配列のリファレンスを返す
sub func {
  my $nums = [1, 2, 3];

  return $nums;
}

# 戻り値としてハッシュのリファレンスを返す
sub func {
  my $score = {math => 100, english => 90};

  return $score;
}
</pre>

<h4>単独のreturn</h4>

単独のreturnは、スカラーコンテキストでは、未定義値 undef、リストコンテキストでは、空リスト () を、 を返します。

<pre>
sub func_name {
  # 処理 ...
  return;
}
</pre>

未定義値を返したい場合は「単独のreturn」を使うのが、慣習になっています。「return undef」とするのは利点もあるのですが、利用頻度は少ないようです。

<h3>デフォルト値の設定</h3>

サブルーチンの引数にデフォルト値を指定しておきたい場合があります。そのような場合、引数が、未定義ならば、デフォルト値を設定するという風に記述します。

<pre>
sub num {
  my $num = shift;
  unless (defined $num) {
    $num = 0;
  } 
}
</pre>

Perl 5.10からは便利な「defined-or演算子」が導入されたので次のように書くこともできます。左辺が未定義だった場合は、右辺の値が代入されます。

<pre>
sub num {
  my $num = shift;
  $num //= 0;
}
</pre>

<h3>エラー処理</h3>

サブルーチンにおけるエラー処理の方法を解説します。

<h4>undefを返す方法</h4>

一番簡単な方法はサブルーチンが失敗したときに「undef」を返す方法です。単独のreturnを使ってundefを返します。

<pre>
sub foo {
  
  my $error;
  
  # ...
  
  if ($error)
    return;
  }
}
</pre>

呼び出し側では、戻り値をチェックします。

<pre>
my $ret = foo();

unless (defined $ret) {
  # エラー処理
}
</pre>

<h4>例外を投げる方法</h4>

お勧めの方法は、エラーが発生したときに例外を投げるものです。die関数を使って例外を投げます。

<pre>
sub foo {
  
  my $error;
  
  # ...
  
  if ($error)
    die "Error";
  }
}
</pre>

もしモジュールの中で例外を投げる場合は、Carpモジュールのcroak関数を利用しましょう。呼び出し元の情報がわかりやすいです。

<pre>
use Carp 'croak';

sub foo {
  
  my $error;
  
  # ...
  
  if ($error)
    croak "Error";
  }
}
</pre>

呼び出し側では次のようにして例外をキャッチします。<a href="/blog/20100312126967.html">eval</a>を使うことで、例外を捕獲できます。evalブロックの最後にセミコロンが必要なので注意しましょう。

<pre>
eval {
  foo();
};

if (my $error = $@) {
  # エラー処理
}
</pre>

例外が発生した場合は「$@」という<a href="/blog/20161102147817.html">特殊変数</a>に、エラーの内容が格納されます。「$@」はグローバル変数で上書きされてしまう可能性があるので、すぐにレキシカル変数に代入しておくのがお勧めです。

例外処理についての詳しい解説は「<a href="/blog/20100411127089.html">Perlの「例外処理」の仕組み - エラーの通知と検知</a>」をご覧ください。

<h3>サブルーチン演習</h3>

サブルーチンをきちんと理解するための演習問題を作成しました。

<ul>
  <li>
    <a href="/blog/20080620121405.html">cvs形式の文字列を、配列の配列に変換する</a>
  </li>
</ul>

<blockquote>
<b>学べるテーマ</b>
CSV、配列の配列、ハッシュの配列、最大値と最小値、並び替え、バブルソート、昇順、降順
</blockquote>

<h3>サブルーチンへのリファレンス</h3>

サブルーチンのリファレンスについての解説です。

<ul>
  <li>
    <a href="/blog/20080704121518.html">サブルーチンへのリファレンスを作成する</a>
  </li>
</ul>


<blockquote>
<b>学べるテーマ</b>
サブルーチンのリファレンス、ポリモーフィズム、ディスパッチテーブル、シグナルハンドラ、イベント駆動型プログラミング
</blockquote>

<h3>サブルーチンのテクニック</h3>


<h4>関数の括弧の省略の規則</h4>

標準関数の括弧は使用しても使用しなくてもかまいません。

<pre>
# 括弧あり
print('Hello');

# 括弧なし
print 'Hello';
</pre>

モジュールからインポートした関数の場合も括弧を使用してもしなくてもかまいません。

<pre>
# モジュールからインポートした関数
use Carp 'croak';
croak 'Error';
</pre>

関数の定義が関数を使用する位置より後ろにある場合は括弧が必須になります。

<pre>
# 関数定義が後ろにある場合は括弧は必須
func();

sub func {
  ...
}
</pre>

括弧を省略するかどうかにはこれといった判断基準はありません。「ほかの人が読みやすいかどうか」と「美感」で決めてください。

※プロトタイプとの関係については、プロトタイプは非推奨なので書きません。

<h4>サブルーチンを再定義する(モンキーパッチ)</h4>

Perlでは一度行ったサブルーチンの定義を再定義することが可能です。

<pre>
sub sum { ... }
no warnings 'redefine';
sub sum { ... }
</pre>
サブルーチンを再定義すると、再定義の警告がでます。これを消すのに<strong>no warnings 'redefine';</strong>という記述を行うことで、警告を消すことができます。

サブルーチンを上書きすることは、モンキーパッチとも呼ばれることがあります。バグがあるサブルーチンを発見した場合に、一時的に、サブルーチンを上書きして、パッチを当てるという使い方ができます。

<h4>caller関数 - 実行している関数名を取得</h4>

実行している関数名を取得するにはcaller関数を使用します。caller関数については「<a href="/blog/20080424120904.html">caller関数 - 実行している関数名を取得</a>」をご覧ください。

<h4>呼び出し元のサブルーチンの引数を取得する</h4>

caller関数を使用すれば、関数の呼び出し元のパッケージ名、サブルーチン名などを取得することができます。さらに呼び出し元のサブルーチンの引数を取得することも可能です。

呼び出し元の関数の引数を取得するにはcaller関数をDB名前空間内でリストコンテキストで呼び出します。<b>@DB::args</b> に呼び出しもとの関数の引数が設定されます。DBはデバッグに利用されるモジュールです。名前空間をDBに一時的に変更するので、ブロックで囲っておきましょう。ブロックが終わると元の名前空間に戻ります。

<pre>
sub a2 {
  {
    # DBパッケージに変更
    package DB; 
    
    # caller関数をリストコンテキストで呼び出すと
    my @c = caller 1;
    
    # @DB::argsに引数が格納される    
    my @args = @DB::args;
  }
}
</pre>

<h4>サブルーチンのオートロード</h4>

Perlでは、サブルーチンが定義されていなかった場合に実行される関数を定義するオートロードという機能があります。

<ul>
  <li>
    <a href="/blog/20080709121562.html">サブルーチンのオートロード AUTOLOAD</a>
  </li>
</ul>

<h4>サブルーチンの動的な作成</h4>

Perlではサブルーチンを動的に作成することができます。

<ul>
  <li>
    <a href="/blog/20080716121630.html">サブルーチンの動的な生成</a>
  </li>
</ul>

<h4>クロージャーの作成</h4>

Perlにおける「クロージャー」の作成については「<a href="/blog/20080710121570.html">Perlで「クロージャー」を作成する方法</a>」をご覧ください。

<h3>サブルーチンに関する読み物</h3>

<ul>
  <li>
    <a href="/blog/20080612121341.html">サブルーチンの定義はスコープの影響を受けない</a>
  </li>
  <li>
    <a href="/blog/20080615121350.html">Perlのサブルーチンの柔軟性をC言語と比較してみる</a>
  </li>
  <li>
    <a href="/blog/20080616121363.html">よいサブルーチンを作成するための心がけ</a>
  </li>
  <li>
    <a href="/blog/20080620121405.html">サブルーチンの演習問題</a>
  </li>
</ul>

<h3>Perlの標準関数の一覧</h3>

Perlの標準関数の一覧を見たい場合は「<a href="/blog/20161004147558.html">Perlの関数の一覧</a>」をご覧ください。
