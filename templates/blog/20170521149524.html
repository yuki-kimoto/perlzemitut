<ol class="bread" itemscope itemtype="http://schema.org/BreadcrumbList">
  <li itemprop="itemListElement" itemscope itemtype="http://schema.org/ListItem">
    <a itemprop="item" href="/">
        <span itemprop="name">Perl</span></a>
    <meta itemprop="position" content="1" />
  </li>
  &#8250;
  <li>
    <span itemprop="name">here</span></a>
  </li>
</ol>
<h2>for/while文を使った繰り返し処理</h2>



Perlの繰り返しの構文について解説します。繰り返しの構文を覚えるとプログラムの幅がすごく広がります。

<h3>for文の基礎</h3>

まず最初に繰り返しの構文である<b>for文</b>について解説します。最初に次のサンプルを見てください。これは、0から9までの数を画面に出力するプログラムです。

<pre>
for (my $i = 0; $i < 10; $i++) {
  print "$i\n";
}
</pre>

これの出力結果は以下のようになります。0から9までの数が出力されていますね。

<pre>
0
1
2
3
4
5
6
7
8
9
</pre>

では構文について順番に解説します。for文は以下のような構文になっています。

<pre>
for (ループ変数の初期化; ループする条件; ループ変数の更新) {
  ...
}
</pre>

<h4>1. ループ変数の初期化</h4>

最初の部分はループ変数の初期化です。この部分でループ変数が最初にどの値で始まるかを決めます。ループ変数というのは、ループ内で利用される変数のことだと思ってください。

最初の例をもう一度見てみましょう。「my $i = 0」という部分に注目してください。

<pre>
for (my $i = 0; $i < 10; $i++) {
  print "$i\n";
}
</pre>

ループ変数の初期化部分では、変数宣言を同時に行うことができます。「$i」という変数を宣言し「0」で初期化しています。

この部分を「my $i = 5」とするとどうなるでしょうか。

<pre>
for (my $i = 5; $i < 10; $i++) { ... }
</pre>

「$i」が「5」で初期化されるので、出力結果は以下のようになります。

<pre>
5
6
7
8
9
</pre>

<h4>2. ループする条件</h4>

さて次に、ループする条件について見ていきましょう。

<pre>
for (ループ変数の初期化; ループする条件; ループ変数の更新) {
  ...
}
</pre>

for文では、ループする条件が満たされる間、ブロックが繰り返し実行されます。最初の例を見ると「$i」が「10」より小さいという条件になっています。

<pre>
for (my $i = 0; $i < 10; $i++) {
  print "$i\n";
}
</pre>

ですので「$i」が「10」より小さい間ブロックが実行されます。

この部分を「$i < 7」とするとどうなるでしょうか。

<pre>
for (my $i = 0; $i < 7; $i++) { ... }
</pre>

出力結果は以下のようになります。「$i」が「7」より小さい間ループするので、「6」まで出力されているのがわかります。

<pre>
0
1
2
3
4
5
6
</pre>

<h4>3. ループ変数の更新</h4>

最後に、ループ変数の更新の部分を解説します。

<pre>
for (ループ変数の初期化; ループする条件; ループ変数の更新) {
  ...
}
</pre>

ループ変数の更新の部分では、次のブロックを実行する前に、ループ変数の値を変えることができます。

<pre>
for (my $i = 0; $i < 10; $i++) {
  print "$i\n";
}
</pre>

最初の例では「$i++」と書いています。これは<a href="/blog/20180224151948.html">インクリメント</a>と呼ばれ、$iの値を1増やすことができます。

次のループに進む前に「$i」の値が1増えます。つまりループを進むたびに「1」「2」「3」という風に増えていくことになります。

この部分を「$i += 2」とすればどうなるでしょうか。これは<a href="/blog/20161005147566.html">特殊な代入演算子</a>で「$i」の値を「2」増やすことができます。

<pre>
for (my $i = 0; $i < 10; $i += 2) {
  print "$i\n";
}
</pre>

2づつ増えていきますので、出力結果は以下のようになります。

<pre>
0
2
4
6
8
</pre>

「0」から始まり「2」ずつ増え「10」より小さい間ループしていることを確認してください。

<h3>for文のサンプルプログラム</h3>

繰り返しの構文はいくつかのサンプルを実際に動かしてみると理解が早まると思います。

<h4>「9」から初めて「0」まで出力する</h4>

次は「9」から初めて「0」まで出力するプログラムを見てみましょう。

<pre>
for (my $i = 9; $i >= 0; $i--) {
  print "$i\n";
}
</pre>

「my $i = 9」と初期化されているので「$i」は「9」から始まりますね。条件は「$i >= 0」なので「$i」が「0以上」のとき繰り返されます。最後の「$i--」で、次のループに移る前に「$i」の値をひとつ減らしています。「--」というのは変数の値を「1」減らす<a href="/blog/20180226151973.html">デクリメント演算子</a>です。

出力結果は以下のようになります。

<pre>
9
8
7
6
5
4
3
2
1
0
</pre>

<h4>ふたつ飛ばしで「9」から初めて「1」まで出力する</h4>

次はふたつ飛ばしで「9」から「1」まで出力してみます。

<pre>
for (my $i = 9; $i >= 0; $i -= 2) {
  print "$i\n";
}
</pre>

ループ変数の更新の部分が「$i -= 2」となっているので、次のループに入る前に「$i」の値が「2」小さくなります。

出力結果は以下のようになります。

<pre>
9
7
5
3
1
</pre>

<h4>for文とif文を組み合わせる</h4>

for文とif文を組み合わせてみましょう。for文とif文を組み合わせられるようになるとプログラミングが上達していきます。

「0」から「10」までループしますが、ループ変数が3の倍数だったときだけ、出力するというサンプルプログラムを書いてみます。

<pre>
for (my $i = 0; $i < 10; $i++) {
  if ($i % 3 == 0) {
    print "$i\n";
  }
}
</pre>

if文を使った条件分岐の部分について注目してみてください。「$i % 3 == 0」という記述で、「$i」が「3」の倍数であることを確認できます。「3」で割った余りが「0」だった場合に、その数は3の倍数といえるからです。

剰余演算子については以下の記事を参考にしてください。

<ul>
  <li>
    <a href="/blog/20080910122105.html">数値計算を行う演算子</a>
  </li>
</ul>

3の倍数の時だけif文のブロックが実行されるので出力結果は以下のようになります。

<pre>
0
3
6
9
</pre>

if文とfor文を組み合わせると、プログラムをうまく制御できるようになります。if文についての詳しい解説は以下の記事を参考にしてください。

<ul>
  <li>
    <a href="/blog/20161023147731.html">if文を使った条件分岐の構文を覚えよう</a>
  </li>
</ul>

<h3>while文</h3>

次は<a href="/blog/20100123126425.html">while文</a>について学びましょう。while文は、for文と同じようにループを行うための構文です。

for文と異なるのは、while文は「ループする条件」の部分しか持たないことです。次のサンプルを見てください。

<pre>
my $i = 0;

while ($i < 10) {
  
  print "$i\n";
  
  $i++;
}
</pre>

これを実行すると「0」から「9」までの数が画面に出力されます。

<pre>
0
1
2
3
4
5
6
7
8
9
</pre>

「$i < 10」という部分がwhile文でループする条件の部分です。for文と違って、初期化の部分やループ変数の更新の部分は、while文にはありません。

<pre>
while (ループする条件) { ... }
</pre>

「ループ変数の初期化」と「ループ変数の更新」の部分は、Perlの文として書く必要があります。

<pre>
# ループ変数の初期化
my $i = 0;

while ($i < 10) {
  
  print "$i\n";
  
  # ループ変数の更新
  $i++;
}
</pre>

上記のwhile文のサンプルは、次のfor文と対応しています。

<pre>
for (my $i = 0; $i < 10; $i++) {
  print "$i\n";
}
</pre>

for文の方がシンプルに書けていますね。ループ変数が必要な場合はfor文の方が書きやすいです。

<h3>foreach文</h3>

<b>foreach文</b>は<a href="/blog/20161012147627.html">配列</a>の要素を順番に処理するときに利用できます。次のサンプルプログラムを見てください。

<pre>
my @animals = ('Cat', 'Dog', 'Mouse');

foreach my $animal (@animals) {
  print "$animal\n";
}
</pre>

これは('Cat', 'Dog', 'Mouse')という配列の要素を順番出力するプログラムです。出力結果は以下のようになります。

<pre>
Cat
Dog
Mouse
</pre>

<h4>for文とforeach文と同じ</h4>

Perlでは、foreach文はfor文の別名になっていて、foreachと書くべきところをforと書くこともできます。

<pre>
my @animals = ('Cat', 'Dog', 'Mouse');

for my $animal (@animals) {
  print "$animal\n";
}
</pre>

最近のPerlの書き方を見ているとforeachよりは常にforを使う傾向があるように思います。短いほうに落ち着くという感じでしょうかね。

<h4>範囲演算子と一緒に使う</h4>

<a href="/blog/20170520149523.html">範囲演算子</a>と一緒に使うこともできます。

<pre>
for my $num (3 .. 6) {
  print "$num\n";
}
</pre>

<h3>next文</h3>

<a href="/blog/20170513149580.html">next文</a>を使うと次の繰り返しの先頭にジャンプすることができます。次のサンプルプログラムを見てください。

<pre>
my @nums = (3, 5, 6, 7);
for my $num (@nums) {
  if ($num == 5) {
    next;
  }
  print "$num\n";
}
</pre>

(3, 5, 6, 7)という配列の要素を出力していますが「$num」が「5」の時だけ次の繰り返しの先頭にジャンプします。出力結果は以下のようになります。

<pre>
3
6
7
</pre>

「5」の時だけ、ループの先頭にジャンプしているので「print関数」が実行されません。

<h3>last文</h3>

<a href="/blog/20170527149589.html">last文</a>を使うと繰り返しを終了して、for文を脱出することができます。次のサンプルプログラムを見てください。

<pre>
my @nums = (3, 5, 6, 7);
for my $num (@nums) {
  if ($num == 6) {
    last;
  }
  print "$num\n";
}
</pre>

(3, 5, 6, 7)という配列の要素を出力していますが「$num」が「6」の時にループを脱出します。そのため、出力結果は以下のようになります。

<pre>
3
5
</pre>

ここではforと組み合わせる例を紹介しましたが、nextとforはwhile文の中でも使うことができます。

<h3>forとwhileとforeachの使い分け</h3>

三つの繰り返しの構文について解説しましたが「実際はどうやって使い分けたらいいの」と感じることと思います。そこで、使い分けの指針について書いておきます。

<h4>まずはforeachで書けないか考える</h4>

まず最初にforeachを使って書けないかを考えます。foreachはループを最も簡単に書けるので、foreachで十分なのであれば、foreachを使います。配列の要素を順番に処理するというのは、頻繁にでてきますが、これはforeachで書けます。

<pre>
my @animals = ('Cat', 'Dog', 'Mouse');

foreach my $animal (@animals) {
  print "$animal\n";
}
</pre>

あるいはエイリアスのforを使って以下のように書きます。

<pre>
my @animals = ('Cat', 'Dog', 'Mouse');

for my $animal (@animals) {
  print "$animal\n";
}
</pre>

まずはこの方法で書けないかを検討します。

<h4>要素番号が必要なときは通常のfor文</h4>

次に要素番号が必要な場合があるとします。そのときは通常のfor文を使います。次のサンプルプログラムは、要素番号と配列の要素を出力するものになっています。「@animals」をスカラコンテキストで評価すると、配列の長さが取得できることに注意してください。

<pre>
my @animals = ('Cat', 'Dog', 'Mouse');

for (my $i = 0; $i < @animals; $i++) {
  my $animal = $animals[$i];
  print "$i : $animal\n";
}
</pre>

出力結果は以下のようになります。

<pre>
0 : Cat
1 : Dog
2 : Mouse
</pre>

<h4>要素番号を増やしていかないような繰り返しはwhile文</h4>

最後に要素番号を増やしていかないような繰り返しであればwhile文を使いましょう。よくあるサンプルをいくつか書いておきます。

<b>1. ファイルから行を読み込む</b>

ファイルから行を読み込む場合はwhile文を使用します。

<pre>
while (my $line = <>) {
  print $line;
}
</pre>

ファイル入出力については以下で詳しく解説しています。

<ul>
  <li>
    <a href="/blog/20110212130370.html">ファイル入出力の基礎</a>
  </li>
</ul>

<b>2. 配列をpopする</b>

配列をpop関数を使って取り出してなくなったら終わりというような処理はwhile文で書きます。

<pre>
my @animals = ('Cat', 'Dog', 'Mouse');

while (my $animal = pop @animals) {
  print "$animal\n";
}
</pre>

出力結果は以下になります。

<pre>
Mouse
Dog
Cat
</pre>

popされて空になったら、ループが終了します。

<b>3. 無限ループを書きたい</b>

無限ループを書きたいときはwhile文を使用します。条件を1にすると、while文をずっと実行され続けます。

<pre>
while (1) {
  # 処理
}
</pre>

何らかの条件が満たされた場合に、無限ループから脱出したい場合はlastを使いましょう。

<pre>
while (1) {
  # 処理
  
  if ($flag) {
    last;
  }
}
</pre>

<h3>多次元データ構造と繰り返し</h3>

最後に多次元データ構造と繰り返しのサンプルプログラムを一つ書いておきます。Perlにおいては、ハッシュの配列というデータ構造を頻繁に使うので、そのサンプルを書いておきます。

<pre>
# ハッシュの配列のサンプルプログラム
my $students = [
  {name => 'kimoto', age => 36},
  {name => 'kana', age => 21},
  {name => 'jiro', age => 56}
];

for my $student (@$students) {
  my $name = $student->{name};
  my $age = $student->{age};
  
  print "Name : $name, Age : $age\n";
}
</pre>

出力結果は以下のようになります。

<pre>
Name : kimoto, Age : 36
Name : kana, Age : 21
Name : jiro, Age : 56
</pre>

ここではまだ一度も解説していない「[]」や「{}」「@$students」「$student->{name}」というものが登場しています。これはリファレンスと呼ばれるPerlの構文です。多次元データ構造の処理については、以下で詳しく解説していますので、参考にしてください。

<ul>
  <li>
    <a href="/blog/20100930127859.html">リファレンスと多次元データ構造</a>
  </li>
</ul>

<h3>ループを簡単に書くための関数</h3>

<h4>grep関数</h4>

Perlには<a href="/blog/20100119126425.html">grep関数</a>というものがあり、配列の要素の中でマッチするものだけを取り出すことができます。

<pre>
my @matches = grep { $_ > 5 } @values;
</pre>

これは、for文で次のように書くのと同じですが、より簡潔です。

<pre>
my @matches;
for my $value (@$values) {
  if ($value > 5) {
    push @matches, $value;
  }
}
</pre>

<h4>map関数</h4>

Perlには<a href="/blog/20100118126425.html">map関数</a>というものがあり、すべての配列の要素の変換を簡単に書くことができます。

<pre>
my @values2 = map { "$_.txt" } @values1;
</pre>

これは、for文で次のように書くのと同じですが、より簡潔です。

<pre>
my @values2;
for my $value1 (@values1) {
  my $value2 = "$value1.txt";
  push @values2, $value2;
}
</pre>

<h3>後置のfor - for修飾子</h3>

特殊なforの構文として、forを文の後ろに置くことができるfor修飾子というものがあります。1行でfor文が書ける場合には、この書き方もシンプルです。

以下は、配列のすべての要素を出力するサンプルです。配列の要素は順番にデフォルト引数である「$_」に代入されます。

<pre>
my @values = (3, 4, 5);
print "$_\n" for @values;
</pre>

読みやすさを考えると通常のfor文を常に使うようにするのがよいと思います。

でも実は、後置のforには明らかな利点があります。それは、<b>後置のforはスコープを作らないので、通常のforよりもパフォーマンスがよい</b>ということです。

日常の業務で書くほとんどのプログラムにおいては、パフォーマンスが問題にならないでしょう。でも、1行で実行できるもので、速さをどうしても稼ぎたい場合は、後置のforを使うとよいでしょう。

