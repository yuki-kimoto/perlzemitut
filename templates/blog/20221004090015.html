<ol class="bread" itemscope itemtype="http://schema.org/BreadcrumbList">
  <li itemprop="itemListElement" itemscope itemtype="http://schema.org/ListItem">
    <a itemprop="item" href="/">
        <span itemprop="name">Perl</span></a>
    <meta itemprop="position" content="1" />
  </li>
  &#8250;
  <li itemprop="itemListElement" itemscope itemtype="http://schema.org/ListItem">
    <a itemprop="item" href="/blog/20161030147778.html">
        <span itemprop="name">オブジェクト指向</span></a>
    <meta itemprop="position" content="2" />
  </li>
  &#8250;
  <li itemprop="itemListElement" itemscope itemtype="http://schema.org/ListItem">
      <span itemprop="name">入門</span>
    <meta itemprop="position" content="3" />
  </li>
</ol>

<h2>Perlオブジェクト指向プログラミング入門</h2>

Perlオブジェクト指向プログラミングの入門記事です。

<h3>クラスの定義</h3>

Perlでは<a href="/blog/20220923131702.html">クラス</a>の定義は<strong>package</strong>を使って行います。

<pre>
package Point;
</pre>

<h3>コンストラクタ</h3>

<a href="/blog/20090317123712.html">コンストラクタ</a>である<b>new</b>という<a href="/blog/20220930115720.html">クラスメソッド</a>を定義します。<a href="/blog/20100203126425.html">bless関数</a>を使って、オブジェクトを生成します。

<pre>
package Point;

sub new {
  my $class = shift;
  
  # オブジェクトの生成
  my $self = bless {@_}, $class;
  
  # 初期化処理
  
  return $self;
}
</pre>

コンストラクタを定義すれば、オブジェクトを作成することができます。

<pre>
my $point = Point->new;
</pre>

<b>new</b>メソッドは、<a href="/blog/20161013147635.html">ハッシュ</a>を受け取ることができます。

<pre>
my $point = Point->new(x => 1, y => 2);
</pre>

この値は、フィールドに設定されます。

<h4>初期化処理</h4>

コンストラクタでの初期化処理は以下のように書きます。

<pre>
sub new {
  my $class = shift;
  
  # オブジェクトの生成
  my $self = bless {@_}, $class;
 
  # 初期化処理
  $self->{x} //= 0;
  $self->{y} //= 0;

  return $self;
}
</pre>

<a href="/blog/20120702134129.html">//演算子</a>を使って初期化しています。

<h3>アクセッサ</h3>

次に<a href="/blog/20090321123713.html">アクセッサ</a>を定義してみましょう。アクセッサとはフィールドにアクセスするための<a href="/blog/20220929115717.html">メソッド</a>のことです。アクセッサを定義すれば、フィールドにアクセスすることができます。

<pre>
# xを取得・設定するアクセッサ
sub x {
  my $self = shift;
  
  if (@_) {
    $self->{x} = $_[0];
    return $self;
  }
  
  return $self->{x};
}
</pre>

アクセッサを定義するとフィールドにアクセスすることができます。

<pre>
# 値の設定
$point->x(1);

# 値の取得
my $x = $point->x;
</pre>

<h3>継承</h3>

<a href="/blog/20090325123747.html">継承</a>を行うには<a href="/blog/20100425127089.html">baseモジュール</a>を使います。<b>Point</b>を継承した<b>Point3D</b>を作ってみましょう。

<pre>
package Point3D;

use base 'Point';
</pre>

<h3>スーパークラスのメソッドの呼び出し</h3>

<a href="/blog/20220922134115.html">SUPER疑似クラス</a>を使って、スーパークラスのメソッドを呼び出すことができます。

<pre>
package Point3D;
use base 'Point';

# ...

sub clear {
  my $self = shift;
  
  # スーパークラスのclearメソッドの呼び出し
  $self->SUPER::clear;
}
</pre>

<h3>コンストラクタのオーバーライド</h3>

サブクラスでコンストラクタを<a href="/blog/20221001064718.html">オーバーライド</a>したい場合があると思います。コンストラクタをオーバーライドするには次のようにします。

<pre>
package Point3D;
use base 'Point';

sub new {
  my $class = shift;
  
  my $self = $class->SUPER::new(@_);
  
  # 初期化処理
  $self->{z} //= 0;

  return $self;
}
</pre>

newはコンストラクタなので必ずオブジェクト自身を返すようにします。

<h3>クラスの作成 - 実践編</h3>

Perlのオブジェクト指向プログラミングを実践してみましょう。<b>Point</b>クラスと<b>Point3D</b>クラスを作成してみます。

<h4>Pointクラス</h4>

<blockquote>
<ul>
  <li>
    Pointは点を表すクラスです。
  </li>
  <li>
    xとyというフィールドがあり、アクセッサを持ちます。
  </li>
  <li>
    xとyの値を0にクリアするclearというメソッドを持ちます。
  </li>
</blockquote>
</ul>

<pre>
package Point;

sub new {
  my $class = shift;
  
  # オブジェクトの生成
  my $self = bless {@_}, $class;
 
  # 初期化処理
  $self->{x} //= 0;
  $self->{y} //= 0;

  return $self;
}

# xを取得・設定するアクセッサ
sub x {
  my $self = shift;
  
  if (@_) {
    $self->{x} = $_[0];
    return $self;
  }
  
  return $self->{x};
}

# yを取得・設定するアクセッサ
sub y {
  my $self = shift;
  
  if (@_) {
    $self->{y} = $_[0];
    return $self;
  }
  
  return $self->{y};
}

sub clear {
  my $self = shift;
  
  $self->x(0);
  $self->y(0);
}

1;
</pre>

末尾に「1;」という記述があるのは、<a href="/blog/20161130148051.html">モジュール</a>を書くファイルの末尾が真でなければならないというルールがあるためです。

Pointクラスは以下のように使用することができます。

<pre>
use Point;
my $point = Point->new(x => 3, y => 5);
print $point->x;
$point->y(9);
$point->clear;
</pre>

<h4>Point3Dクラス</h4>

<blockquote>
<ul>
  <li>
    Point3Dは3次元の点を表すクラスです。
  </li>
  <li>
    x,y,zというフィールドがあり、アクセッサを持ちます。
  </li>
  <li>
    x,y,zの値を0にクリアするclearというメソッドを持ちます。
  </li>
</blockquote>
</ul>

<b>Point3D</b>は<b>Point</b>を継承して作成します。<b>clear</b>メソッドは<b>x</b>と<b>y</b>と<b>z</b>の値をクリアするためにオーバーライドされています。
    
<pre>
package Point3D;
use base 'Point';

# zを取得・設定するアクセッサ
sub z {
  my $self = shift;
  
  if (@_) {
    $self->{z} = $_[0];
    return $self;
  }
  
  return $self->{z};
}

sub clear {
  my $self = shift;
  
  $self->SUPER::clear;
  
  $self->z(0);
}

1;
</pre>

Point3Dクラスは以下のように利用することができます。

<pre>
use Point3D;
my $point = Point->new(x => 3, y => 5, z => 8);
print $point->z;
$point->z(9);
$point->clear;
</pre>

<h3>実行できるサンプル</h3>

実行できるサンプルを書いておきますね。

<h4>lib/Point.pm</h4>

libというディレクトを作成して、その中にPoint.pmというファイルを作成してください。

<pre>
package Point;

use strict;
use warnings;

sub new {
  my $class = shift;
  
  # オブジェクトの生成
  my $self = bless {@_}, $class;
 
  # 初期化処理
  $self->{x} //= 0;
  $self->{y} //= 0;

  return $self;
}

# xを取得・設定するアクセッサ
sub x {
  my $self = shift;
  
  if (@_) {
    $self->{x} = $_[0];
    return $self;
  }
  
  return $self->{x};
}

# yを取得・設定するアクセッサ
sub y {
  my $self = shift;
  
  if (@_) {
    $self->{y} = $_[0];
    return $self;
  }
  
  return $self->{y};
}

sub clear {
  my $self = shift;
  
  $self->x(0);
  $self->y(0);
}

1;
</pre>

<h4>lib/Point3D.pm</h4>

libというディレクトの中にPoint3D.pmというファイルを作成してください。

<pre>
package Point3D;
use base 'Point';

# zを取得・設定するアクセッサ
sub z {
  my $self = shift;
  
  if (@_) {
    $self->{z} = $_[0];
    return $self;
  }
  
  return $self->{z};
}

sub clear {
  my $self = shift;
  
  $self->SUPER::clear;
  
  $self->z(0);
}

1;
</pre>

<h4>object.pl</h4>

クラスを読み込んで利用するサンプルです。libモジュールを使うことで、モジュールの検索パスを追加できます。

<pre>
use strict;
use warnings;
use lib 'lib';
use Point3D;

my $point = Point3D->new(x => 3, y => 5, z => 8);

print $point->z . "\n";

$point->z(9);

print $point->z . "\n";
$point->clear;

print $point->z . "\n";
</pre>

<h4>スクリプトの実行</h4>
ディレクトリ構成は以下のようにします。

<pre>
object.pl
lib - Point.pm
    - Point3D.pm
</pre>

以下のように実行してください。

<pre>
perl object.pl
</pre>

出力結果です。

<pre>
8
9
0
</pre>

<h3>オブジェクト指向プログラミングの詳細</h3>

オブジェクト指向プログラミングの詳細については、以下の記事を見てください。

<ul>
  <li><a href="/blog/20161030147778.html">Perlオブジェクト指向プログラミング</a>
</ul>
