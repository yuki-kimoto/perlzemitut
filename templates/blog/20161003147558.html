<ol class="bread" itemscope itemtype="http://schema.org/BreadcrumbList">
  <li itemprop="itemListElement" itemscope itemtype="http://schema.org/ListItem">
    <a itemprop="item" href="/">
        <span itemprop="name">Perl</span></a>
    <meta itemprop="position" content="1" />
  </li>
  &#8250;
  <li>
    here
  </li>
</ol>
<h2>Perlの文字列処理の基礎</h2>



Perlの文字列処理についての解説です。Perlはテキスト処理が得意な言語です。文字列の出力、ヒアドキュメント、検索、置換、書式指定などをわかりやすく解説します。また日本語を扱うための方法についても解説します。

<h3>文字列の基礎</h3>

まずは最初に文字列の基礎を解説します。

<h4>文字列の作り方</h4>

最初に文字列を作成してみましょう。文字列を作成するには<b>シングルクォート</b>あるいは<b>ダブルクォート</b>を使用します。

<pre>
# シングルクォートで文字列を作成
my $str1 = 'Hello';

# ダブルクォートで文字列を作成
my $str2 = "Hello";
</pre>

<h4>ダブルクォートで囲った場合</h4>

文字列を作成するときはシングルクォートで囲むか、ダブルクォートで囲みますが、ダブルクォート演算子で囲った場合は、<b>エスケープシーケンス</b>と<b>変数展開</b>を使うことできます。

<ul>
  <li>
    <b>エスケープシーケンスが使える</b>
  </li>
  <li>
    <b>変数展開が使える</b>
  </li>
</ul>

<h4>エスケープシーケンス</h4>

<b>エスケープシーケンス</b>とは、ダブルクォートで囲った文字列で使える特殊文字のことです。

たとえば<b>改行「\n」</b>や<b>タブ「\t」</b>を使うことができます。改行やタブは目には見えない文字ですが、エスケープシーケンスを使って表現することができます。

<pre>
# 改行
my $str1 = "Hello\n";

# タブ文字
my $str2 = "Cat\tDog";
</pre>

よく使うエスケープシーケンスは改行とタブなので、この二つは必ず覚えておきましょう。

「\」という記号はエスケープのために利用されます。「\」という記号自体を表現したい場合は「\\」とします。

<pre>
# 「\」を表現 - 「\\」とする
my $str1 = "Hello \\ World"
</pre>

<b>シングルクォートやダブルクォートを使いたい場合</b>

シングルクォートで囲った文字列の中で、シングルクォートを使いたい場合や、ダブルクォートで囲った文字列の中で、ダブルクォートを使いたい場合も、エスケープシーケンスを使います。

<pre>
# シングルクォートを使う
my $str1 = 'Kimoto\'s cat';

# ダブルクォートを使う
my $str2 = "Can't open \"foo.txt\".";
</pre>

<h4>変数展開</h4>

<b>変数展開</b>とは、変数をダブルクォートの中で囲まれた文字列の中で使用できる機能のことです。変数に含まれている文字列が展開されます。

<pre>
my $str1 = "Dog";

# 変数展開 - 「I have Dog」と変数が展開される
my $str2 = "I have $str1";
</pre>

変数展開はPerlのとても便利な機能なので、どんどん使いましょう。

<b>変数展開が失敗する場合</b>

変数展開を使っていると、ときどき変数展開が失敗して、コンパイルエラーになることがあると思います。それは、後ろに続く文字列も変数の一部だと解釈されてしまう場合です。

たとえば、変数の後ろに「アンダーバー」が続く場合や「日本語」が続く場合などです。

<pre>
my $str1 = "Dog";

# $str1を展開したいが変数名が「$str1_Foo」と解釈されてしまう
my $str2 = "I have $str1_Foo";

# $str1を展開したいが変数名が「$str1あいうえお」と解釈されてしまう
my $str3 = "I have $str1あいうえお";
</pre>

このような場合は変数名を明示するために「${変数名}」という記法を使うことができます。

<pre>
${変数名}
</pre>

先ほどの例であれば、次のように書くとうまくいきます。

<pre>
my $str1 = "Dog";

my $str2 = "I have ${str1}_Foo";

my $str3 = "I have ${str1}あいうえお";
</pre>

<h4>文字列の連結</h4>

次に文字列を連結してみましょう。文字列を連結するには<a href="/blog/20080221120361.html">文字列連結演算子「.」</a>を使用します。

<pre>
my $str1 = 'I have a' . 'pen';
</pre>

上記の例はクォートで囲まれた文字列を連結していますが、変数を使うこともできます。

<pre>
my $str3 = $str1 . $str2;
</pre>

文字列連結演算子は代入演算子と組み合わせて次のように使うこともできます。

<pre>
$str1 .= $str2;
</pre>

これは次の書き方と同じになります。「$str1」の後ろに「$str2」を連結して「$str1」に代入するという意味です。よく利用する書き方なので覚えておきましょう。

<pre>
$str1 = $str1 . $str2;
</pre>

<h4>文字列の出力</h4>

文字列を画面に出力するには<a href="/blog/20170404149131.html">print関数</a>を使用します。

<pre>
my $message = 'Hello';
print $message;
</pre>

<h3>文字列処理の小技</h3>

ときどき使われる文字列処理の小技をいくつか紹介しておきます。

<h4>文字列リスト演算子</h4>

Perlでは、文字列のリストを簡単に作るための<b>文字列リスト演算子</b>があります。

文字列リスト演算子を使うと、クォートとカンマを省略して文字列のリストを作成することができます。

<pre>
# 通常の方法
my @strs1 = ('Cat', 'Dog', 'Mouse');

# 文字列リスト演算子を使う方法
my @strs2 = qw(Cat Dog Mouse);
</pre>

この演算子が特に便利なのは、利用したい単語が、テキストファイルに何行も続いている場合です。

<pre>
Cat
Dog
Mouse
</pre>

文字列リスト演算子を使うとコピペするだけで、Perlスクリプトの中で使うことができます。

<pre>
my @strs = qw(
  Cat
  Dog
  Mouse
);
</pre>

文字列リスト演算子の詳しい解説は以下を参考にしてください。

<ul>
  <li>
    <a href="/blog/20080222120369.html">文字列リスト演算子</a>
  </li>
</ul>

<h4>改行を取り除く</h4>

テキストファイルを読み込んだ時に<b>末尾の改行を取り除きたい</b>場合があります。

Perlでは改行を取り除くための<a href="/blog/20080226120403.html">chomp関数</a>があるのですが、この関数は環境依存です。

Windows系OSで実行した場合はWindowsの改行である「CR+LF」を取り除き、Unix/Linux/Macで実行した場合は「LF」を取り除きます。

確実に改行を取り除くためには、以下の<a href="/blog/20100827127859.html">正規表現</a>を使うのがよいでしょう。

<pre>
$str =~ s/\x0D?\x0A?$//;
</pre>

正規表現についてはこの後で簡単に解説します。

<h4>指定した文字で配列の要素を連結する</h4>

文字列処理ではCSVデータ(カンマで区切られたデータ)を作成したり、TSVデータ(タブで区切られたデータ)を作成することが頻繁にあります。

このような場合は、配列にデータを準備して、それを特定の文字(カンマやタブ)で連結するということを行います。

<b>join関数</b>を使うとこれが簡単にできます。

<pre>
my @animals = ('Cat', 'Dog', 'Mouse');

# CSVデータ「Cat,Dog,Mouse」を作成
my $csv = join(',', @animals);

# TSVデータ「Cat  Dog  Mouse」を作成
my $tsv = join("\t", @animals);
</pre>

join関数については以下の記事を参考にしてください。

<ul>
  <li>
    <a href="/blog/20081007122338.html">join関数 - 区切り文字を指定して文字列を連結する</a>
  </li>
</ul>

<h4>指定した文字で文字列を分割する</h4>

上記では、CSVデータとTSVデータの作成方法を解説しましたが、その反対に、CSVデータやTSVデータを配列にしたい場合があると思います。

このような場合は<b>split関数</b>を使用すると、特定の文字を区切り文字に指定して配列を作成することができます。

<pre>
my $csv = 'Cat,Dog,Mouse';

# カンマを指定して配列を作成。('Cat', 'Dog', 'Mouse')という配列になる
my @animals = split(/,/, $csv);
</pre>

TSVデータの場合のサンプルも載せておきます。

<pre>
my $tsv = "Cat\tDog\tMouse";
my @animals = split(/\t/, $csv);
</pre>

split関数の第一引数には、正規表現が直接指定できます。たとえば「一つ以上の空白」という正規表現を使うことができます。

<pre>
# ひとつ以上の空白で区切られたデータを配列にする
my $data = "Cat    Dog  Mouse";
my @animals = split(/ +/, $csv);
</pre>

正規表現については、この後に簡単に解説します。

split関数の詳しい解説については以下の記事をご覧ください。

<ul>
  <li>
    <a href="/blog/20080301120434.html">split関数 - 指定した文字で文字列を分割する</a>
  </li>
</ul>

<h4>文字列の長さを取得する</h4>

文字列の長さを取得するには<b>length関数</b>を使用します。

<pre>
# 文字列の長さを取得
my $length = length "ABCDE";
</pre>

length関数の詳細な解説については以下をご覧ください。

<ul>
  <li>
    <a href="/blog/20080926122245.html">length関数 - 文字列の長さを取得する</a>
  </li>
</ul>

<h4>文字列の書式指定</h4>

小数点の桁数の指定や、左を0で埋めるような書式指定を行いたい場合は<b>sprintf関数</b>を利用できます。

<pre>
# 3桁で左を0埋め「Code is 013」となる
my $str1 = sprintf("Code is %03d", 13);

# 小数点第3位まで表示「Number is 0.146」となる
my $str2 = sprintf("Number is %.3f", 0.145677);
</pre>

書式指定の詳細な解説については以下をご覧ください。

<ul>
  <li>
    <a href="/blog/20081001122296.html">sprintf関数 - 書式指定した文字列を作成する</a>
  </li>
</ul>

<h3>ヒアドキュメント</h3>

Perlには複数行の文字列を簡単に作成するための<b>ヒアドキュメント</b>という構文があります。

<pre>
# ヒアドキュメント
my $message = <<EOS;
If you want to write
multiple lines,
you can use here document
EOS
</pre>

ヒアドキュメントの構文の説明をします。ヒアドキュメントは「<<」という記号で始まります。その語に文字列の末尾を指定する文字列を指定します。

これは任意の文字列でよいのですが、僕は「End of String」の略で「EOS」という記号を使っています。

この後にセミコロン「;」を書くことに注意してください。

そしてこの次の行から文字列を開始します。

文字列を終わりたいときは、最後の行に「EOS」と記述します。EOSの前には、空白を置くことができないので注意してください。

もし、インデントされてい位置からヒアドキュメントを書きたい場合は次のように書きます。

<pre>
        # ヒアドキュメント
        my $message = <<EOS;
If you want to write
multiple lines,
you can use here document
EOS
</pre>

<h4>ダブルクォートあるいはシングルクォートで囲った文字列と同じ意味にする</h4>

ヒアドキュメントを次のように書いた場合は、ダブルクォートで囲った場合と同じで、エスケープシーケンスと特殊文字を使うことができます。

<pre>
my $message = <<EOS;
...
EOS
</pre>

あるいは

<pre>
my $message = <<"EOS";
...
EOS
</pre>

下の例は、EOSをダブルクォートで囲っていることに注意してください。ダブルクォートで囲まない場合はデフォルト、ダブルクォートで囲ったのと同じ意味になります。

シングルクォートで囲った場合と同じようにするには次のように書きます。

<pre>
my $message = <<'EOS';
...
EOS
</pre>

ヒアドキュメントの詳しい解説については以下の記事を参考にしてください。

<ul>
  <li>
    <a href="/blog/20080619121405.html">ヒアドキュメント - 複数行の文字列を簡単に作成</a>
  </li>
</ul>

<h3>正規表現</h3>

Perlには文字列の検索と置換を行うための<b>正規表現</b>という機能が言語に組み込まれています。

<h4>パターンマッチを使って検索を行う</h4>

たとえば、文字列の中に「dog」という文字列が含まれていることを知りたい場合は次のように書けます。

<pre>
my $message = "It is my dog\n";

if ($message =~ /dog/) {
  ...
}
</pre>

<b>=~</b>はパターンマッチ演算子と呼ばれ正規表現のパターンマッチを実行するための演算子です。

正規表現は「//」という記号で囲む必要があります。

<h4>置換を行う</h4>

正規表現を使うと置換を行うこともできます。「dog」を「cat」に書き換えた場合は次のように書きます。

<pre>
my $message = "It is my dog\n";

$message =~ s/dog/cat/;
</pre>

<b>「s/置換する文字列/置換後の文字列/」</b>という構文で置換を行うことができます。

正規表現の詳しい解説については以下の記事をご覧ください。

<ul>
  <li>
    <a href="/blog/20100827127859.html">実戦で役立つ正規表現</a>
  </li>
</ul>

<h3>Perlで正しく日本語を扱う</h3>

ここまで文字列のサンプルはすべて英語でした。でも実際の文字列処理は、日本語を扱うことが大半だと思います。

初級者の方は、Perlで日本語処理を正しく行うにはどうしたらいいのかということを感じると思います。

たとえば、length関数や正規表現は、内部的な文字列に変換してから実行しないと、正しい結果を返しません。

「日本語で正規表現がうまく動かないなぁ」というときは、Perlで日本語を正しく扱えていない可能性があります。

でも少しのことを覚えれば、Perlの文字列処理は決して難しくありませんので安心してください。

Perlで正しく日本語処理を行うためには、次の三つのことを覚えましょう。

<ol>
  <li>
    ソースコードはUTF-8で記述し、utf8プラグマを指定する。
  </li>
  <li>
    外部から受け取った文字列は必ずデコードする
  </li>
  <li>
    外部へ出力する文字列は、エンコードする
  </li>
</ol>

<h4>ソースコードはUTF-8で記述し、utf8プラグマを指定する。</h4>

ソースコードはWindowsでも、Macでも、LinuxでもUTF-8で保存しましょう。そして<b>utf8</b>プラグマを指定しましょう。

<pre>
# utf8プラグマを指定
use utf8;

my $message = 'こんにちは';
</pre>

まずこれを約束事として覚えてください。

これでソースコードに書かれた文字列に対して、length関数や正規表現が正しく動くようになります。

<pre>
# utf8プラグマを指定
use utf8;

my $message = 'こんにちは';

# length関数が正しく動く。5が返る。
my $length = length $message;

# 正規表現が正しく動く
if ($message =~ /にち/) {
  ...;
}
</pre>

utf8プラグマは、ソースコード内に書かれた「UTF-8バイト文字列」を「内部文字列」に変換します。

内部文字列に変換することによって、正しく文字列処理を行うことができるようになります。

「バイト文字列」と「内部文字列」についてはこの後で解説します。

<h4>外部から受け取った文字列は必ずデコードする</h4>

<b>デコード</b>というのは「特定の文字コードで書かれた文字列」を「Perlの内部的な文字列」に変換することを言います。

「Perlの内部的な文字列」のことを<b>内部文字列</b>と便宜的に呼ぶことにし、「特定の文字コードで書かれた文字列」のことを<b>バイト文字列</b>と呼ぶことにします。

たとえば「UTF-8」という文字コードで書かれている文字列を「UTF-8バイト文字列」と呼び、「EUC-jp」という文字コードで書かれている文字列を「EUC-jpバイト文字列」と呼びます。

もしJavaで文字列の扱いを知っているなら「文字列ストリーム」と「バイトストリーム」という対応で考えるとわかりやすいです。

デコードを行うためには<a href="/blog/20091118124667.html">Encodeモジュール</a>のdecode関数を使用します。

<b>Windowsで「cp932」という文字コードを書かれたファイルを読み込む</b>

Windowsで「cp932」という文字コードを書かれたファイルを読み込むサンプルを書いてみます。

<pre>
# decode.pl
use strict;
use warnings;
use utf8;
use Encode 'decode';

# ファイルの読み込み
while (my $line = <>) {
  $line = decode('cp932', $line);
  
  if ($line =~ /田中/) {
    print "OK";
  }
}
</pre>

<a href="/blog/20100123126425.html">while文</a>を使って、コマンドラインの引数で指定されたファイルから1行づつ読みだしています。

ファイル「name.txt」のサンプルは以下のようになっています。cp932で保存してください。

<pre>
田中
鈴木
山田
</pre>

次のようにコマンドを実行すると一度だけOKと表示され正規表現が正しく動くことがわかります。

<pre>
perl decode.pl name.txt
</pre>

<b>Windowsでコマンドライン引数の文字列を読み込む</b>

外部から受け取った文字列というのは、ファイルだけではなく、コマンドライン引数や環境変数など、プログラムの外から読み込まれるものはすべて外部です。

ですから、コマンドライン引数で日本語を受け取った場合も、必ずデコードします。

<pre>
my $name = $ARGV[0];
$name = decode('cp932', $name);
</pre>

<h4>外部へ出力する文字列は、エンコードする</h4>

外部から受け取った文字列はデコードするのに対し、外部へ出力する文字は必ずエンコードします。

エンコードとは「内部文字列」を「バイト文字列」に変換することを言います。

エンコードを行うには<a href="/blog/20091118124667.html">Encodeモジュール</a>のencode関数を使用します。

たとえば、UTF-8で出力したい場合は次のように書きます。

<pre>
use utf8;
use Encode 'encode';

my $message = 'あいうえお';

# 出力する前にエンコード
print encode('UTF-8', $message);
</pre>

<b>ファイル名を指定する場合</b>

もうひとつ覚えておいてほしいのは、open関数などでファイル名を指定する場合は、OSで利用されている文字コードにエンコードする必要があります。

日本語の名前を持つファイルを開くには次のようにします。エラーメッセージに日本語ファイル名を持つ場合は、その部分もエンコードしましょう。

<pre>
use utf8;
use Encode 'encode';

my $file = 'テスト.txt';

# 日本語名のファイルをopenする
open my $fh, '<', encode('cp932', $file)
  or die encode('cp932', "Can't open file \"$file\": $!");
</pre>

これは少し面倒だと感じると思います。よく使う場合は、サブルーチンやモジュール化しておくのも、楽にする方法のひとつかなと思います。

Perlで日本語を扱う詳細な解説についてはEncodeモジュールの解説で行っていますので、以下の記事をご覧ください。

<ul>
  <li>
    <a href="/blog/20091118124667.html">Encodeモジュール - 日本語などのマルチバイト文字列を適切に処理する</a>
  </li>
</ul>

<h3>文字列にかかわる演算子</h3>

文字列処理のために時々使う演算をここに掲載しておきます。

<ul>
  <li>
    <a href="/blog/20100304126967.html">クォート演算子「q」 - クォートの代替表現</a>
  </li>
  <li>
    <a href="/blog/20100305126967.html">ダブルクォート演算子「qq」 - ダブルクォートの代替表現</a>
  </li>
</ul>

<h3>文字列処理のための関数</h3>

文字列処理のためにときどき使う関数を掲載しておきます。

<table>
  <tr>
    <td>
      <a href="/blog/20080224120386.html">substr関数</a>
    </td>
    <td>
      指定した位置の文字を抽出,置換する
    </td>
  </tr>
  <tr>
    <td>
      <a href="/blog/20080225120395.html">index関数</a>
    </td>
    <td>
      文字列を検索する
    </td>
  </tr>
  <tr>
    <td>
      <a href="/blog/20100321126967.html">rindex関数</a>
    </td>
    <td>
      文字列を末尾から検索する
    </td>
  </tr>
  <tr>
    <td>
      <a href="/blog/20080930122278.html">reverse関数</a>
    </td>
    <td>
      文字列の順序を反転させる
    </td>
  </tr>
  <tr>
    <td>
      <a href="/blog/20100325127089.html">ucfirst関数</a>
    </td>
    <td>
      文字列の先頭の文字を大文字に変換する
    </td>
  </tr>
  <tr>
    <td>
      <a href="/blog/20100327127089.html">lcfirst関数</a>
    </td>
    <td>
      文字列の先頭の文字を小文字に変換する
    </td>
  </tr>
  <tr>
    <td>
      <a href="/blog/20080903122057.html">uc関数</a>
    </td>
    <td>
      小文字を大文字に変換する
    </td>
  </tr>
  <tr>
    <td>
      <a href="/blog/20100326127089.html">lc関数</a>
    </td>
    <td>
      大文字を小文字に変換する
    </td>
  </tr>
  <tr>
    <td>
      <a href="/blog/20080928122260.html">chr関数</a>
    </td>
    <td>
      数値をASCIIコード対応する文字に変換する
    </td>
  </tr>
  <tr>
    <td>
      <a href="/blog/20080929122278.html">ord関数</a>
    </td>
    <td>
      ASCIIコードの文字を内部表現である数値に変換する
    </td>
  </tr>
</table>

<h3>まとめ</h3>

これだけ覚えておけば、日本語を含んだ文字列処理の9割くらいは行えるようになるんじゃないかと思います。

<h3>Perlの文字列の動画解説を見る</h3>

<ul>
  <li><a href="https://www.youtube.com/watch?v=785mATXYHnM&feature=youtu.be">文字列とは - Perlプログラミングちゃんねる 2020</a></li>
  <li><a href="https://www.youtube.com/watch?v=Nx3SGnkBmO8">文字列の基礎 - Perlプログラミングちゃんねる 2020</a></li>
  <li><a href="https://www.youtube.com/watch?v=F-hiLU-6yjE&feature=youtu.be">シングルクォート文字列 - Perlプログラミングちゃんねる 2020</a></li>
  <li><a href="https://www.youtube.com/watch?v=6ZlMPXi1zqk&feature=youtu.be">ダブルクォート文字列 - Perlプログラミングちゃんねる 2020</a></li>
</ul>
